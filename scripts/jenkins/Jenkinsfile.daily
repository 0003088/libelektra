/* https://build.libelektra.org/jenkins/job/elektra-jenkinsfile-daily/ */

// Set properties for buildjob
properties([
    // Discard all but the latest 30 logs
    buildDiscarder(logRotator(numToKeepStr: '30', artifactNumToKeepStr: '30')),

    // Don't run concurrently
    disableConcurrentBuilds()
])

// Stages
stage("Maintain Reference Repositories") {
    parallel generateRepositoryMaintanenceStages()
}

stage("Maintain docker nodes") {
    parallel generateDockerMaintanenceStages()
}

stage("Maintain docker repositories") {
    parallel generateDockerRepoMaintanenceStages()
}

stage("Cleanup Files") {
    parallel generateFileCleanupStages()
}

// Generators
def generateRepositoryMaintanenceStages() {
    // Get all nodes with label 'gitmirror' and the master
    // DO NOT TAG master WITH 'gitmirror' as it will only show up as (null)
    def nodes = nodesByLabel label:"gitmirror"
    nodes.add("master")

    def repos = [
        [
            url: "https://github.com/ElektraInitiative/libelektra.git",
            dest: "libelektra"
        ]
    ]

    def tasks = [:]
    nodes.each { target_node ->
        repos.each { target_repo ->
            tasks << mirrorRepoOnNode(target_node, target_repo)
        }
    }
    return tasks
}

def mirrorRepoOnNode(target_node, target_repo) {
    def taskname = "git/${target_node}/${target_repo.dest}"
    return [(taskname): {
        stage(taskname) {
            node(target_node) {
                syncRepository target_repo
            }
        }
    }]
}

def generateDockerMaintanenceStages() {
    def nodes = nodesByLabel label:"docker"
    def tasks = [:]

    nodes.each { target_node ->
        tasks << cleanupDockerImages(target_node)
    }
    return tasks
}

def cleanupDockerImages(target_node) {
    def taskname = "cleanupDocker/${target_node}"
    return [(taskname): {
        stage(taskname) {
            node(target_node) {
                dockerImages().each {
                    retainNewestImages(it, 15)
                }
                sh "docker system prune -f"
            }
        }
    }]
}

def generateDockerRepoMaintanenceStages() {
    def nodes = nodesByLabel label:"docker&&registry"
    def tasks = [:]

    nodes.each { target_node ->
        tasks << cleanupDockerRegistry(target_node)
    }
    return tasks
}

def cleanupDockerRegistry(target_node) {
    def taskname = "cleanupDockerRegistry/${target_node}"
    return [(taskname): {
        stage(taskname) {
            node(target_node) {
                withCredentials([usernameColonPassword(credentialsId:
                'docker-hub-elektra-jenkins', variable: 'AUTH')]) {
                    docker.image('anoxis/registry-cli')
                          .withRun('-l $AUTH -r https://hub.libelektra.org --delete')
                }
                def frontend_folder = "${env.HOME}/compose/frontend"
                dir(frontend_folder){
                    sh 'docker-compose run registry bin/registry garbage-collect /etc/docker/registry/config.yml'
                }
            }
        }
    }
}

def generateFileCleanupStages() {
    def tasks = [:]
    tasks << cleanupCoverage()
    return tasks
}

def cleanupCoverage() {
    def cleanup_command = """
find /srv/libelektra/coverage/ \
    -mindepth 1 \
    -maxdepth 1 \
    -type d \
    -ctime +30 \
    -print0 \
    | xargs -r -0 rm -R
"""
    def taskname = "cleanup_coverage"
    return [(taskname): {
        stage(taskname) {
            node {
                sshExec(
                    'doc.libelektra.org',
                    cleanup_command
                )
            }
        }
    }]
}

// Helpers
def sshExec(remoteConfig, command) {
    sshPublisher(publishers: [
        sshPublisherDesc(
            configName: remoteConfig,
            transfers: [
                sshTransfer(
                    execCommand: command
                )
            ],
            verbose: true
        )
    ])
}

def dockerImages() {
    def s = /docker images --format "{{.Repository}}" | awk '!seen[$0]++'/
    def r = sh returnStdout: true, script: s
    return r.tokenize('\n')
}

def retainNewestImages(image, n) {
  sh "docker images ${image} --format \"{{.Repository}} {{.Tag}}\" | sort -k 2,2 | head -n -${n} | awk \'BEGIN{OFS=\":\"} {print \$1,\$2}' | xargs -r -n1 docker rmi"
}

def syncRepository(Map repo) {
    dir("${env.HOME}/git_mirrors") {
        if(fileExists(repo.dest)) {
            echo "Found existing repository mirror"
            dir(repo.dest){
                sh "git fetch --prune"
            }
        } else {
            echo "Create new repository mirror"
            sh "git clone --mirror ${repo.url} ${repo.dest}"
        }
    }
}
